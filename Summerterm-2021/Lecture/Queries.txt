Unter http://od.fmi.uni-leipzig.de läuft ein RDF Triple Store, über den alle
Informationen zu den Lehrveranstaltungen der Fakultät Mathematik und Informatik
im RDF Format verwaltet werden.

Mehr zur Struktur dieser RDF Datenbasis siehe
http://bis.informatik.uni-leipzig.de/OLAT/LVPlanung/ODQuelle/ 

Dieser Triple Store bietet auch einen SPARQL Endpunkt an, über den jedermann
Anfragen als SPARQL Queries an diese Datenbasis stellen kann

Dieser Endpunkt ist unter 
http://od.fmi.uni-leipzig.de:8892/sparql
zu erreichen.

Es folgen einige Beispiele für derartige Anfragen an die Lehrplanung.

------------------------------------------------------------------

-- einfache Anfrage nach allen LV mit URI-Präfix SWS.IADW

PREFIX od: <http://od.fmi.uni-leipzig.de/model/>
SELECT distinct ?l ?name ?d ?b ?r
from <http://od.fmi.uni-leipzig.de/w19/>
WHERE {
  ?l a od:LV .
  ?l rdfs:label ?name .
  ?l od:beginsAt ?b .
  ?l od:dayOfWeek ?d . 
  ?l od:locatedAt ?r . 
filter regex(?l,'SWS.IADW') .
}

-- Anfrage nach allen LV, die der Unit SWS.IADW zugeordnet sind 

PREFIX od: <http://od.fmi.uni-leipzig.de/model/>
SELECT distinct ?l ?name ?d ?b ?r
from <http://od.fmi.uni-leipzig.de/studium/>
from <http://od.fmi.uni-leipzig.de/w19/>
WHERE {
  <http://od.fmi.uni-leipzig.de/studium/SWS.IADW> od:containsKurs ?k .
  ?k od:containsLV ?l . 
  ?l rdfs:label ?name .
optional{ ?l od:beginsAt ?b .}
optional{ ?l od:dayOfWeek ?d . }
optional{ ?l od:locatedAt ?r . }
}

-- Anfrage nach allen LV, die der Unit SWS.IADW zugeordnet sind, mit Auflösung
   der Namen der Räume und Lehrkräfte

PREFIX od: <http://od.fmi.uni-leipzig.de/model/>
SELECT distinct ?l ?name ?d ?b ?rn ?pn
from <http://od.fmi.uni-leipzig.de/studium/>
from <http://od.fmi.uni-leipzig.de/w19/>
from <http://od.fmi.uni-leipzig.de/rooms/>
from <http://od.fmi.uni-leipzig.de/Personal/>
WHERE {
  <http://od.fmi.uni-leipzig.de/studium/SWS.IADW> od:containsKurs ?k .
  ?k od:containsLV ?l . 
  ?l rdfs:label ?name .
  ?l od:beginsAt ?b .
  ?l od:dayOfWeek ?d . 
  ?l od:locatedAt ?r . 
  ?r rdfs:label ?rn . 
  ?l od:servedBy ?p . 
  ?p foaf:name ?pn . 
}

--------------------------------------------------

-- RDF Teilgraphen mit relevanten Informationen extrahieren
-- Verwende Ansicht Turtle (beautiful)

PREFIX od: <http://od.fmi.uni-leipzig.de/model/>
Construct { ?l ?p ?o . ?o ?p1 ?o1 . }
from <http://od.fmi.uni-leipzig.de/s21/>
from <http://od.fmi.uni-leipzig.de/rooms/>
from <http://od.fmi.uni-leipzig.de/Personal/>
WHERE {
  ?l a od:LV .
  ?l ?p ?o .
optional { ?o ?p1 ?o1 . }
filter regex(?l,'BIS') .
}

@prefix s21:    <http://od.fmi.uni-leipzig.de/s21/> .
@prefix rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix od:	<http://od.fmi.uni-leipzig.de/model/> .
@prefix odp:	<http://od.fmi.uni-leipzig.de/personal/> .
@prefix odr:	<http://od.fmi.uni-leipzig.de/rooms/> .
@prefix rdfs:	<http://www.w3.org/2000/01/rdf-schema#> .
@prefix foaf:	<http://xmlns.com/foaf/0.1/> .

